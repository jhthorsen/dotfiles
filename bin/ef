#!/usr/bin/env perl
use strict;
use warnings;

$ENV{EDITOR} //= 'vim';
my $list = 0;
@ARGV = grep { $list ||= $_ eq '-l'; $_ ne '-l' } @ARGV;

if (-t STDIN) {
  die "Usage: ef [-l] <ack-args>\n" unless @ARGV;
  open my $ACK, '-|', ack => -l => @ARGV or die "ack -l @ARGV: $!";
  process($_) while <$ACK>
}
else {
  process($_ = /(\S+)/ ? $1 : $_) while <>;
}

sub process {
  local $_ = shift;
  chomp;

  # Return unless the file exists
  return unless -e;

  # Print the file name when "-l" is given as argument
  return print "$_\n" if $list;

  # Edit the file
  my $pid = fork;
  return waitpid $pid, 0 if $pid;

  close STDIN;
  open STDIN, '<', '/dev/tty' or die "/dev/tty: $!";
  exec $ENV{EDITOR} => $_;
  exit $!;
}
