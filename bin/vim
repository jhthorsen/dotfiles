#!/usr/bin/env perl
use strict;
use Cwd 'abs_path';

# Load dynamic %ENV, which can be use by nvim plugins
my $project_dir = -d '.git';
for my $file ('../.vim.env', '.vim.env') {
  next unless -f $file;
  warn $@ unless eval "use Dotenv qw(-load $file);1";
  $project_dir = 1;
}

my $curr = abs_path $0;
my @bin;
push @bin, $ENV{VIM_BIN} if $ENV{VIM_BIN};
@bin = grep { $_ ne $curr } map { chomp; abs_path $_ } qx'which -a vim' unless @bin;
die "Could not find real vim" unless @bin;

# Normal vim
do { exec $bin[0] => @ARGV } or die $! if @ARGV or !$project_dir;

# Vim from project directory without arguments
my $file = qx"fzf --preview 'head -100 {}'";
chomp $file;
do { exec $bin[0] => $file } or die $! if $file;
