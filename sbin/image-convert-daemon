#!/usr/bin/env perl
use Mojo::Base -strict;

use Linux::Inotify2;
use Mojo::JSON qw(encode_json);
use Mojo::Log;
use Time::HiRes qw(sleep);

use Mojo::File qw(path);
sub tempfile { Mojo::File::tempfile(SUFFIX => sprintf '.%s', shift) }

my $root   = shift @ARGV or die "Usage: $0 /path/to/image/dir\n";
my $ext_re = qr{\.(pdf|png|jpe?g)$}i;

my $log = Mojo::Log->with_roles('+Color')->new(level => 'debug');
my $inotify = Linux::Inotify2->new or die "Unable to construct a Linux::Inotify2 object: $!";
my (%mtime, %watched);

$root = path $root;
watch_directory($root);
watch_directory($_) for grep { -d } $root->list({dir => 1})->each;
$inotify->poll while 1;

sub convert_file {
  my ($file, $rules) = @_;
  return pdfsplit($file, $rules) if $file->extname eq 'pdf';

  my $ext = $rules->{ext} || $file->extname;
  $ext = 'jpg' if $ext eq 'jpeg';
  my $tmp = tempfile($ext);
  my $size0 = (stat $file)[7];

  imagemagick($file, $tmp, $rules);
  jpegoptim($tmp, $rules) if $ext eq 'jpg';
  pngcrush($tmp, qw(-rem allb -brute -reduce)) if $ext eq 'png';

  if (-s $tmp) {
    $file->remove;
    $file = "$file";
    $file =~ s!\.\w+$!.$ext!;
    $tmp->move_to($file);
  }

  my $size1 = (stat $file)[7];
  $log->info("Converted $file => size=$size0/$size1");
}

sub imagemagick {
  my ($file, $tmp, $rules) = @_;
  my @args;
  push @args, '-resize' => $rules->{dimensions} if $rules->{dimensions};
  return $file unless @args;
  return $file unless run(convert => @args => $file => $tmp);
  return $tmp;
}

sub jpegoptim {
  my ($file, $rules) = @_;
  my @args = qw(-q -p);
  push @args, "-s"                      if $rules->{strip};
  push @args, "--max=$rules->{quality}" if $rules->{quality};
  push @args, "--size=$rules->{size}"   if $rules->{size};
  run(jpegoptim => @args, $file);
}

sub pdfsplit {
  my ($file, $rules) = @_;
  my $out = "$file";

  $out =~ s!\.pdf$!!i;
  run(convert => -density => 160, "$file", -background => 'white', -alpha => 'remove', "$out-%04d.png");
  run(montage => -mode => 'concatenate', -tile => $rules->{tile}, "$out-0*.png", "$out-$rules->{tile}.png") if $rules->{tile};

  $log->info("Converted $file => $out-%%%%.png");
}

sub pngcrush {
  my ($file, @args) = @_;
  my $tmp = tempfile($file->extname);
  return run(pngcrush => @args => $file => $tmp) ? $tmp : $file;
  $tmp->move_to($file);
}

sub parse_rules {
  local $_ = shift;
  my %rules;

  $rules{dimensions} = $1 if /(?:^|\s)(\d*x\d+|\d+x\d*)/;
  $rules{ext}        = $1 if /\b(?:ext|format|type)=([a-z]{2,5})/;
  $rules{quality}    = $1 if /\b(?:q|quality)=(\d+)/;
  $rules{size}       = $1 if /\b(?:s|size)=(\d+)\b/;
  $rules{strip}      = 1  if /\bstrip\b/;
  $rules{tile}       = $1 if /\btile=(\w+)/;

  return \%rules;
}

sub run {
  my @cmd = @_;
  $log->debug(join ' ', map { /\s/ ? qq('$_') : $_ } @cmd);
  system @cmd;
  return $? ? 0 : 1;
}

sub watch_directory {
  my $path = shift;
  return if $watched{$path};

  my $rules = parse_rules($path->basename);
  return unless %$rules;

  $watched{$path} = $inotify->watch("$path", IN_ALL_EVENTS, sub {
    my $e = shift;

    my $file = path($e->fullname);
    return watch_directory($file) if -d $file;
    return unwatch_directory($file) if !-e $file;

    # Check that we have a valid file that has not already been processed
    return unless $file->basename =~ $ext_re;
    return if $file->basename =~ m!^\.!;
    return unless my $mtime = eval { $file->stat->mtime };
    return if $mtime{$file} and $mtime{$file} + 30 >= $mtime;

    my $size = $file->stat->size || -1;
    my $guard = 20;
    while ($guard--) {
      sleep 1;
      next unless my $curr_size = $file->stat->size;
      last if $size == $curr_size;
      $size = $curr_size;
    }

    $log->info("Going to convert $file => size=$size");
    convert_file($file, $rules);
    $mtime{$file} = $mtime;
  });

  $log->info(sprintf 'Watching "%s" with rules %s', $path, encode_json($rules));
}

sub unwatch_directory {
  my $path = shift;
  return unless my $watch = delete $watched{$path};
  $watch->cancel;
  $log->info(sprintf 'Not watching "%s" anymore', $path);
}
